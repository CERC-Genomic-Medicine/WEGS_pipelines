#!/usr/bin/env nextflow

/*
*AUTHOR: Praveen Nadukkalam Ravindran, PhD <praveen.nadukkalamravindran@mcgill.ca>
*VERSION: 1.0
*YEAR: 2021
*/

bams = Channel.from(file(params.bamsListPath).readLines()).map { line -> fields = line.split(); [ fields[0], file(fields[1]), file(fields[1]).getBaseName() + ".bai", file(fields[2]), file(fields[2]).getBaseName() + ".bai"] }

// To make sure that sample names are consistent across the read groups and also between the wes and wgs bam files.
process RenameSampleName {
   cache "lenient"
   cpus 1
   memory "4 GB"
   time "1h"
   errorStrategy 'retry'
   maxRetries 3
   
   input:
   tuple val(sample), file('wes_bam.bam'), file('wes_bam.bai'), file('wgs_bam.bam'), file('wgs_bam.bai') from bams
   
   output:
   tuple val(sample), file('bam1.bam'), file('bam2.bam') into bams_to_merge
   tuple val(sample), file('bam1.bam') into wes_bam
   tuple val(sample), file('bam1.bam') into wes_before_merge_stats
   tuple val(sample), file('bam2.bam') into (wgs_bam_autosomal, wgs_before_merge_stats)

   """
   samtools view -H wes_bam.bam | sed "s/SM:[^\t]*/SM:${sample}/g" | samtools reheader - wes_bam.bam > bam1.bam
   samtools view -H wgs_bam.bam | sed "s/SM:[^\t]*/SM:${sample}/g" | samtools reheader - wgs_bam.bam > bam2.bam
   """
}

wes_bam.combine(Channel.from(file(params.bedsListPath).readLines()).map { line -> fields = line.split(); [ fields[0], file(fields[1])] }).into { wes_bam_target; wes_bam_flanking }

//Merge the wes and wgs bam files and mark duplicates
process MergeAndMarkDuplicates {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "6h"
   errorStrategy 'retry'
   maxRetries 3
   

   input:
   tuple val(sample), file(bam1), file(bam2) from bams_to_merge

   output:
   tuple file("${sample}.dedup.bam"),file("${sample}.dedup.bai"),file("${sample}.marked_dup_metrics.txt") into bams_merged_dedup
   tuple val(sample), file("${sample}.dedup.bam") into merged_bam
   tuple val(sample), file("${sample}.dedup.bam") into after_merge_stats
   tuple val(sample), file("${sample}.dedup.bam") into bam_to_cram


   publishDir "${params.resultFolder}"

   """
   java -jar -Xmx8G -XX:ParallelGCThreads=1 $EBROOTPICARD/picard.jar MarkDuplicates CREATE_INDEX=true I=${bam1} I=${bam2} O=${sample}.dedup.bam M=${sample}.marked_dup_metrics.txt SORTING_COLLECTION_SIZE_RATIO=0.05 MAX_RECORDS_IN_RAM=100000 COMPRESSION_LEVEL=3 VALIDATION_STRINGENCY=STRICT
   """
}

merged_bam.combine(Channel.from(file(params.bedsListPath).readLines()).map { line -> fields = line.split(); [ fields[0], file(fields[1])] }).into { merged_bam_target; merged_bam_flanking }

//Convert the merged bam file to cram file. Provide the reference genome file used for the bam files.
process GenerateCramFromBam {
   cache "lenient"
   cpus 1
   memory "8 GB"
   time "4h"
   errorStrategy "finish"

   input:
   tuple val(sample),file(mergedBam) from bam_to_cram

   output:
   tuple val(sample),file("${sample}.dedup.cram") into cram_for_indexing

   publishDir "${params.resultFolder}", pattern: "${sample}.dedup.cram"

   """
   samtools view -C -T ${params.refGenome} ${mergedBam} > ${sample}.dedup.cram
   """
}

//Index the cram files generated by the GenerateCramFromBam process.
process IndexCrams {
   cache "lenient"
   cpus 1
   memory "8 GB"
   time "2h"
   errorStrategy "finish"

   input:
   tuple val(sample), file(cram) from cram_for_indexing

   output:
   file("${sample}.dedup.cram.crai") into cram_indexes

   publishDir "${params.resultFolder}", pattern: "${sample}.dedup.cram.crai"

   """
   samtools index ${cram} ${sample}.dedup.cram.crai
   """
}

//Genearate samtools stats for the wes bam file.
process GenerateStatsBeforeMergeWes {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 
   input:
   tuple val(sample), file(bam1) from wes_before_merge_stats

   output:
   file "${sample}-wes.stats" into wes_bef_merge_stats

   publishDir "${params.resultFolder}", pattern: "${sample}-wes.stats"

   """
   samtools stats ${bam1} > ${sample}-wes.stats
   """
}

//Genearate samtools stats for the wgs bam file.
process GenerateStatsBeforeMergeWgs {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 
   input:
   tuple val(sample), file(bam2) from wgs_before_merge_stats

   output:
   file "${sample}-wgs.stats" into wgs_bef_merge_stats

   publishDir "${params.resultFolder}", pattern: "${sample}-wgs.stats"

   """
   samtools stats ${bam2} > ${sample}-wgs.stats
   """
}

//Genearate samtools stats for the merged bam file.
process GenerateStatsAfterMerge {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 
   input:
   tuple val(sample), file(mergedBam) from after_merge_stats

   output:
   file "${sample}-merged.stats" into aft_merge_stats

   publishDir "${params.resultFolder}", pattern: "${sample}-merged.stats"

   """
   samtools stats ${mergedBam} > ${sample}-merged.stats
   """
}

//Genearate custom depth stats for flanking regions (+/- 10bp target regions) in the wes bam file.
process GenerateCustomStatsFlankingWes {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 

   input:
   tuple val(sample), file(bam1), val(bedLabel), file(bed) from wes_bam_flanking

   output:
   file "${sample}-wes.${bedLabel}.flanking.depth" into wes_flanking_depth

   publishDir "${params.resultFolder}", pattern: "${sample}-wes.${bedLabel}.flanking.depth"

   """
   bedtools merge -i ${bed} > ${bedLabel}.adjmerge.bed
   bedtools flank -i ${bedLabel}.adjmerge.bed -g ${params.bedGenomePath} -b 10 > ${bedLabel}.adjmerge.flanking.bed
   samtools depth -a -b ${bedLabel}.adjmerge.flanking.bed -q ${params.baseQuality} -Q ${params.mappingQuality} -s ${bam1} | python ${params.projectDir}/customStats.py -o ${sample}-wes.${bedLabel}.flanking.depth
   """
}

//Genearate custom depth stats for target regions in the wes bam file.
process GenerateCustomStatsTargetWes {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 

   input:
   tuple val(sample), file(bam1), val(bedLabel), file(bed) from wes_bam_target

   output:
   file "${sample}-wes.${bedLabel}.depth" into wes_target_depth

   publishDir "${params.resultFolder}", pattern: "${sample}-wes.${bedLabel}.depth"

   """
   bedtools merge -i ${bed} > ${bedLabel}.adjmerge.bed
   samtools depth -a -b ${bedLabel}.adjmerge.bed -q ${params.baseQuality} -Q ${params.mappingQuality} -s ${bam1} | python ${params.projectDir}/customStats.py -o ${sample}-wes.${bedLabel}.depth
   """
}

//Genearate custom depth stats for flanking regions (+/- 10bp target regions) in the merged bam file.
process GenerateCustomStatsFlankingMerged {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 

   input:
   tuple val(sample), file(mergedBam), val(bedLabel), file(bed) from merged_bam_flanking

   output:
   file "${sample}-merged.${bedLabel}.flanking.depth" into merged_flanking_depth

   publishDir "${params.resultFolder}", pattern: "${sample}-merged.${bedLabel}.flanking.depth"

   """
   bedtools merge -i ${bed} > ${bedLabel}.adjmerge.bed
   bedtools flank -i ${bedLabel}.adjmerge.bed -g ${params.bedGenomePath} -b 10 > ${bedLabel}.adjmerge.flanking.bed
   samtools depth -a -b ${bedLabel}.adjmerge.flanking.bed -q ${params.baseQuality} -Q ${params.mappingQuality} -s ${mergedBam} | python ${params.projectDir}/customStats.py -o ${sample}-merged.${bedLabel}.flanking.depth
   """
}

//Genearate custom depth stats for target regions in the merged bam file.
process GenerateCustomStatsTargetMerged {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 
   input:
   tuple val(sample), file(mergedBam), val(bedLabel), file(bed) from merged_bam_target

   output:
   file "${sample}-merged.${bedLabel}.depth" into merged_target_depth

   publishDir "${params.resultFolder}", pattern: "${sample}-merged.${bedLabel}.depth"


   """
   bedtools merge -i ${bed} > ${bedLabel}.adjmerge.bed
   samtools depth -a -b ${bedLabel}.adjmerge.bed -q ${params.baseQuality} -Q ${params.mappingQuality} -s ${mergedBam} | python ${params.projectDir}/customStats.py -o ${sample}-merged.${bedLabel}.depth
   """
}

//Genearate custom depth stats  in the wes bam file.
process GenerateCustomStatsWgs {
   cache "lenient"
   cpus 1
   memory "16 GB"
   time "3h"
   errorStrategy 'retry'
   maxRetries 3
 

   input:
   tuple val(sample), file(bam2) from wgs_bam_autosomal

   output:
   file "${sample}-wgs.autosomal.depth" into wgs_autosomal_depth

   publishDir "${params.resultFolder}", pattern: "${sample}-wgs.autosomal.depth"

   """
   samtools depth -a -q ${params.baseQuality} -Q ${params.mappingQuality} -s ${bam2} | python ${params.projectDir}/customStats.py -a -o ${sample}-wgs.autosomal.depth
   """
}

